group 'com.github.jpmossin'
version '0.2.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: "$System.env.SONATYPE_USER", password: "$System.env.SONATYPE_PASSWORD")
            }

            pom.project {
                name 'hlist'
                packaging 'jar'
                description 'A java.util.List with some useful higher-order functions'
                url 'https://github.com/jpmossin/hlist'

                scm {
                    url 'scm:git@github.com:jpmossin/hlist.git'
                    connection 'scm:git@github.com:jpmossin/hlist.git'
                    developerConnection 'scm:git@github.com:jpmossin/hlist.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jpmossin'
                        name 'Jan-Preben Mossin'
                    }
                }
            }
        }
    }
}
